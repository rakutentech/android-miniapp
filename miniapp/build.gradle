apply from: '../config/android/library.gradle'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    defaultConfig {
        buildConfigField "String", "MINI_APP_BASE_URL", "\"https://www.example.com/\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    buildToolsVersion = androidBuildTool
}

// Prevents test code compiled into binary.
afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    compileOnly "javax.annotation:jsr250-api:$jsr250"

    implementation "androidx.appcompat:appcompat:$androidx_appcompat"
    implementation "androidx.core:core-ktx:$androidx_coreKtx"
    implementation "androidx.work:work-runtime-ktx:$androidx_work"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.jakewharton.timber:timber:$timber"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    api "com.squareup.retrofit2:retrofit:$retrofit"

    kapt "com.google.dagger:dagger-compiler:$dagger"
    implementation "com.google.dagger:dagger:$dagger"

    kapt "com.rakuten.tech.mobile:manifest-config-processor:$manifest_config"
    implementation "com.rakuten.tech.mobile:manifest-config-annotations:$manifest_config"
    implementation "com.rakuten.tech.mobile.sdkutils:sdk-utils:$sdk_utils"

    testImplementation "androidx.test.ext:junit:$androidx_test_ext"
    testImplementation "androidx.work:work-testing:$androidx_work"

    testImplementation "org.mockito:mockito-android:$mockito"
    testImplementation "org.mockito:mockito-core:$mockito"
    testImplementation "org.mockito:mockito-inline:$mockito_inline"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$nhrm_mockito_kotlin"
    testImplementation "org.amshove.kluent:kluent-android:$kluent_android"

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_test"
    testImplementation "org.robolectric:robolectric:$robolectric"
    testImplementation "com.google.truth:truth:$truth"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockwebserver"
}

apply from: "../config/quality/checkstyle/android.gradle"
apply from: '../config/quality/detekt/android.gradle'
dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.1.1"
}

detekt {
    baseline = file("detekt-baseline.xml")
}

apply from: '../config/quality/jacoco/android.gradle'
jacoco {
    toolVersion = "0.8.3"
}

apply from: '../gradle/documentation.gradle'

apply from: '../config/publish/android.gradle'
publishing {
    publications {
        MiniApp(MavenPublication, androidArtifact())
    }
}

def isSnapshot = project.version.contains('-')
if (isSnapshot) {
    apply from: '../config/publish/artifactory.gradle'
} else {
    apply from: '../config/publish/bintray.gradle'
}
