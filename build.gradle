// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.androidBuildTool = '29.0.2'
    ext.androidx_appcompat = '1.1.0'
    ext.androidx_coreKtx = '1.2.0-rc01'
    ext.androidx_espressoCore = '3.3.0-alpha02'
    ext.androidx_test_ext = '1.1.2-alpha02'
    ext.androidx_testRunner = '1.3.0-alpha02'
    ext.androidx_work = '2.3.0-beta02'
    ext.dokka_version = '0.10.0'
    ext.dagger = '2.24'
    ext.jsr250 = '1.0'
    ext.junit = '4.12'
    ext.kotlin_version = '1.3.50'
    ext.mockito = '2.28.2'
    ext.retrofit = '2.6.3'
    ext.robolectric = '4.3'
    ext.timber = '4.7.1'
    ext.truth = '1.0'
    ext.sdk_utils = '0.1.1'

//  Build Config.
    apply from: 'config/index.gradle'
    CONFIG.versions.android.sdk.min = 21

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "digital.wup:android-maven-publish:3.6.2"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }

    repositories {
        google()
        jcenter()
    }
}

plugins {
    id 'io.gitlab.arturbosch.detekt' version '1.1.1'
    id 'net.ltgt.errorprone' version '0.7'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
            options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
    }

    scmVersion {
        useHighestVersion = true
        tag {
            prefix = 'v'
            versionSeparator = ''
            versionIncrementer 'incrementMinor'
        }
    }

    project.version = scmVersion.version
}

tasks.withType(Test) {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 100
    reports.html.enabled = false
    reports.junitXml.enabled = false
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
}
